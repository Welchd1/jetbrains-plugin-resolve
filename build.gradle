plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.15'
    id("org.jetbrains.grammarkit") version "2019.3"
    id 'antlr'
}

intellij {
    version '2019.3.1'
    pluginName = 'jetbrains-plugin-resolve'
    plugins = ['java'] //needs this line or the intellij plugin dev sources don't download...
    downloadSources = true
}

group 'edu.clemson.computing'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compile files('lib/resolve.jar')
    antlr("org.antlr:antlr4:4.6") { // use ANTLR version 4
        exclude group:'com.ibm.icu', module:'icu4j'
    }
    implementation 'org.antlr:antlr4-intellij-adaptor:0.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/

def htmlFixer = {htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '')}

patchPluginXml {
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
    pluginDescription = htmlFixer('src/main/resources/META-INF/description.html')
    sinceBuild = '191'
}